allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    version = '1.8.0'

    group = 'de.bwaldvogel'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'

    ext {
        nettyVersion = '4.1.22.Final'
        mongoJavaDriverVersion = '3.6.3'
        h2Version = '1.4.197'
        springVersion = '5.0.4.RELEASE'
        springDataMongoDbVersion = '2.0.5.RELEASE'
        slf4jVersion = '1.7.25'
        jacksonVersion = '2.9.5'
        junitVersion = '4.12'
        assertjVersion = '2.6.0'
        powerMockVersion = '1.7.3'
        powerMockClassExtensionVersion = '3.2'
        logbackVersion = '1.2.3'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

        testCompile group: 'junit', name: 'junit', version: junitVersion
        testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion
        testCompile group: 'org.powermock', name: 'powermock-api-easymock', version: powerMockVersion
        testCompile group: 'org.easymock', name: 'easymockclassextension', version: powerMockClassExtensionVersion
        testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        testRuntime group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
    }

    test {
        maxHeapSize = "256m"
        systemProperties['io.netty.leakDetectionLevel'] = 'advanced'
    }

}

// https://github.com/bmuschko/gradle-nexus-plugin
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.standardout:gradle-versioneye-plugin:1.5.0'
    }
}

ext {
    title = 'mongo-java-server'
    url = 'https://www.github.com/bwaldvogel/mongo-java-server'
}

jar {
    manifest {
        attributes 'Implementation-Title': title, 'Implementation-Version': version
    }
}

allprojects {
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'org.standardout.versioneye'

    def projectName = project.name

    modifyPom {
        project {
            groupId        'de.bwaldvogel'
            artifactId     projectName
            name           title
            description    'Stub implementation of MongoDB in Java that speaks the wire protocol.'
            url            url
            inceptionYear '2012'

            scm {
                url 'git@github.com:bwaldvogel/mongo-java-server.git'
                connection 'scm:git:git@github.com:bwaldvogel/mongo-java-server.git'
                developerConnection 'scm:git:git@github.com:bwaldvogel/mongo-java-server.git'
            }

            licenses {
                license {
                    name 'The BSD License'
                    url 'http://www.opensource.org/licenses/bsd-license.php'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'bwaldvogel'
                    name 'Benedikt Waldvogel'
                    email 'mail@bwaldvogel.de'
                }
            }
        }
    }

    extraArchive {
        sources = true
        tests = false
        javadoc = true
    }

    nexus {
        sign = true
    }
}

wrapper {
	gradleVersion = "4.8"
	distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    compile project(':mongo-java-server-core')
    compile project(':mongo-java-server-memory-backend')
}

// vim: set ts=4 sw=4 et:
