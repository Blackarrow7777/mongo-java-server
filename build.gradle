allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    version = '1.4.0'

    sourceCompatibility = 1.6

    compileJava.options.encoding = 'UTF-8'

    ext {
        nettyVersion = '4.0.25.Final'
        mongoJavaDriverVersion = '2.13.0'
        slf4jVersion = '1.7.10'
        junitVersion = '4.12'
        festAssertVersion = '1.4'
        powerMockVersion = '1.6.1'
        powerMockClassExtensionVersion = '3.2'
        logbackVersion = '1.1.2'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

        testCompile group: 'junit', name: 'junit', version: junitVersion
        testCompile group: 'org.easytesting', name: 'fest-assert', version: festAssertVersion
        testCompile group: 'org.powermock', name: 'powermock-api-easymock', version: powerMockVersion
        testCompile group: 'org.easymock', name: 'easymockclassextension', version: powerMockClassExtensionVersion
        testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    }

	task copyEclipseSettings(type: Copy) {
		from rootProject.file('eclipse/settings')
		into project.file('.settings')
		include 'org.eclipse.*.prefs'
	}

	tasks["eclipse"].dependsOn copyEclipseSettings

}

// https://github.com/bmuschko/gradle-nexus-plugin
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

ext {
    title = 'mongo-java-server'
    description = 'Stub implementation of MongoDB in Java that speaks the wire protocol.'
    url = 'https://www.github.com/bwaldvogel/mongo-java-server'
}

jar {
    manifest {
        attributes 'Implementation-Title': title, 'Implementation-Version': version
    }
}

allprojects {
    apply plugin: 'nexus'

    def projectName = project.name

    modifyPom {
        project {
            groupId        'de.bwaldvogel'
            artifactId     projectName
            name           title
            description    description
            url            url
            inceptionYear '2012'

            scm {
                url 'git@github.com:bwaldvogel/mongo-java-server.git'
                connection 'scm:git:git@github.com:bwaldvogel/mongo-java-server.git'
                developerConnection 'scm:git:git@github.com:bwaldvogel/mongo-java-server.git'
            }

            licenses {
                license {
                    name 'The BSD License'
                    url 'http://www.opensource.org/licenses/bsd-license.php'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'bwaldvogel'
                    name 'Benedikt Waldvogel'
                    email 'mail@bwaldvogel.de'
                }
            }
        }
    }

    nexus {
        attachSources = true
        attachTests = false
        attachJavadoc = true
        sign = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

dependencies {
    compile project(':mongo-java-server-core')
    compile project(':mongo-java-server-memory-backend')
}

// vim: set ts=4 sw=4 et:
